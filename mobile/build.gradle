apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'

jacoco {
    toolVersion '0.7.9'
}

String VERSION_REVISION = 'git rev-list --count HEAD'.execute().text.trim()
String VERSION_HASH     = 'git rev-parse --short HEAD'.execute().text.trim()

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId    'com.github.ayltai.newspaper'
        minSdkVersion    project.hasProperty('ciBuild') ? 16 : 21
        targetSdkVersion 27
        versionCode      26
        versionName      '3.8.' + VERSION_REVISION + '-' + VERSION_HASH

        testInstrumentationRunner          'android.support.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics : 'true'

        vectorDrawables {
            useSupportLibrary true
        }

        resConfigs 'auto'
    }

    signingConfigs {
        release {
            storeFile     project.file('../key-store.jks')
            keyAlias      System.getenv('ANDROID_KEY_ALIAS')
            storePassword System.getenv('ANDROID_STORE_PASSWORD')
            keyPassword   System.getenv('ANDROID_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            multiDexEnabled     true
            testCoverageEnabled true

            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled   true
            shrinkResources true
            signingConfig   signingConfigs.release
            proguardFiles   getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues     = true

            all {
                jacoco {
                    includeNoLocationClasses true
                }
            }
        }
    }

    lintOptions {
        abortOnError false
        disable     'RestrictedApi'
        disable     'MissingTranslation'
    }

    aaptOptions {
        cruncherEnabled project.hasProperty('ciBuild')
    }

    dexOptions {
        preDexLibraries !project.hasProperty('ciBuild')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

checkstyle {
    configFile     rootProject.file('checkstyle.xml')
    showViolations true
}

configurations {
    codacy
}

ext {
    multidexVersion       = '1.0.2'
    supportLibraryVersion = '27.0.2'
    architectureVersion   = '1.0.0'
    firebaseSdkVersion    = '11.6.2'
    daggerVersion         = '2.13'
    rxBindingVersion      = '2.0.0'
    okhttpVersion         = '3.9.1'
    retrofitVersion       = '2.3.0'
    frescoVersion         = '1.5.0'
    bigImageViewerVersion = '1.4.5'
    exoPlayerVersion      = '2.6.0'
    leakCanaryVersion     = '1.5.4'
    robolectricVersion    = '3.5.1'
    powerMockVersion      = '2.0.0-beta.5'
    espressoVersion       = '3.0.1'
}

repositories {
    flatDir {
        dirs './aars'
    }
}

dependencies {
    implementation fileTree(dir : 'libs', include : [ '*.jar' ])

    // Multi-dex support
    debugImplementation       "com.android.support:multidex:$multidexVersion"
    androidTestImplementation "com.android.support:multidex-instrumentation:$multidexVersion"

    // Android Architecture libraries
    implementation ('android.arch.lifecycle:runtime:1.0.3') {
        exclude group : 'com.android.support'
    }
    implementation ("android.arch.lifecycle:extensions:$architectureVersion") {
        exclude group : 'com.android.support'
    }
    annotationProcessor ("android.arch.lifecycle:compiler:$architectureVersion") {
        exclude group : 'com.android.support'
    }
    implementation "android.arch.lifecycle:common-java8:$architectureVersion"

    // Android Support libraries
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-config:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-perf:$firebaseSdkVersion"

    // Reactive programming
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxBindingVersion"

    // Dependency injection
    implementation      "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // Networking
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation ("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
        exclude group : 'stax'
        exclude group : 'xpp3'
    }

    // Database
    implementation 'io.realm:android-adapters:2.1.1'

    // Image processing
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$frescoVersion"
    implementation "com.github.piasy:BigImageViewer:$bigImageViewerVersion"
    implementation 'com.github.stfalcon:frescoimageviewer:0.5.0'
    implementation "com.android.support:exifinterface:$supportLibraryVersion"
    implementation "com.google.android.gms:play-services-vision:$firebaseSdkVersion"

    // Video processing
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayerVersion"

    // UI components
    implementation 'com.squareup.flow:flow:1.0.0-alpha3'
    implementation 'com.nex3z:flow-layout:1.1.0'
    implementation 'com.roughike:bottom-bar:2.3.1'

    // Eye candies
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'io.supercharge:shimmerlayout:1.0.2'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'pub.hanks:smallbang:1.2.2'
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    implementation 'com.gjiazhe:PanoramaImageView:1.0'
    implementation 'io.github.inflationx:calligraphy3:3.0.0'

    // Code generation tools
    compileOnly         "com.jakewharton.auto.value:auto-value-annotations:1.5"
    annotationProcessor "com.google.auto.value:auto-value:1.5.2"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    implementation      'com.google.code.gson:gson:2.8.2'

    // Fabric
    implementation ('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    // Instabug
    implementation 'com.instabug.library:instabug:4.3.3'

    // Debugging
    implementation        'com.akaita.java:rxjava2-debug:1.2.1'
    debugImplementation   "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation    "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Unit testing
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation "org.powermock:powermock-module-junit4:$powerMockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powerMockVersion"
    testImplementation "org.powermock:powermock-classloading-xstream:$powerMockVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-multidex:$robolectricVersion"
    testImplementation 'org.json:json:20171018'

    // Instrumented testing
    androidTestImplementation (name : 'cloudtestingscreenshotter_lib', ext : 'aar')
    androidTestImplementation ("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude group : 'com.android.support'
    }
    androidTestImplementation ("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group : 'com.android.support'
    }
    androidTestImplementation ("com.android.support.test.espresso:espresso-intents:$espressoVersion") {
        exclude group : 'com.android.support'
    }

    // Code coverage by Codacy
    codacy 'com.github.codacy:codacy-coverage-reporter:2.0.1'
}

configurations.all {
    resolutionStrategy {
        force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

task customFindBugs(type : FindBugs) {
    ignoreFailures = false
    effort         = "max"
    reportLevel    = "medium"
    classes        = files("$project.buildDir/intermediates/classes")

    // Use this only if you want exclude some errors
    excludeFilter = file("$rootProject.rootDir/findbugs-android-exclude.xml")

    source    = fileTree('src/main/java/')
    classpath = files()

    reports {
        xml.enabled      = false
        xml.withMessages = true
        html.enabled     = true
        xml.destination  file("$project.buildDir/outputs/findbugs/findbugs-output.xml")
        html.destination file("$project.buildDir/outputs/findbugs/findbugs-output.html")
    }
}

assemble.dependsOn customFindBugs

def coverageSourceDirs = [
    'src/main/java',
    'src/debug/java'
]

task jacocoTestReport(type : JacocoReport, dependsOn : 'testDebugUnitTest') {
    group       = 'Reporting'
    description = 'Generate JaCoCo coverage reports'

    reports {
        xml.enabled  = true
        html.enabled = true
    }

    classDirectories = fileTree(
        dir      : 'build/intermediates/classes/debug',
        excludes : [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*RealmProxy.*',
            '**/*ColumnInfo.*',
            '**/*RealmModule*.*',
            '**/AutoValue_*.*',
            '**/Dagger*.*',
            '**/*Module_Provide*Factory.*',
            '**/*_Factory.*',
            '**/*_MembersInjector.*',
            '**/*_LifecycleAdapter.*'
        ]
    )

    sourceDirectories = files(coverageSourceDirs)
    executionData     = fileTree(
        dir     : "$buildDir",
        include : [ 'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec' ]
    )

    doFirst {
        files('build/intermediates/classes/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

task uploadCoverageToCodacy(type : JavaExec, dependsOn : jacocoTestReport) {
    main      = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy

    args = [
        "-l",
        "Java",
        "-r",
        "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
    ]
}

task (codacyDepsize) << {
    def size = 0

    configurations.codacy.collect {
        it.length() / (1024 * 1024)
    }.each {
        size += it
    }

    println "Total dependencies size: ${Math.round(size * 100) / 100} MB"

    configurations.codacy.sort {
        -it.length()
    }.each {
        println "${it.name}: ${Math.round(it.length() / (1024) * 100) / 100} KB"
    }
}

task (codacyLocs) << {
    configurations.codacy.each {
        String jarName = it

        println jarName
    }
}

apply plugin: 'com.google.gms.google-services'
